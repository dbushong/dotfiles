#!/usr/bin/perl
#
# nsl - converts IP addresses into names inline in files specified or piped
#       STDIN;
#	with -p also converts :port after the IP
#

use strict;
use warnings;
use Socket;
use Getopt::Std;
use File::Basename;

my $prog = basename($0);
my %opt;
getopts('hp', \%opt);
die <<EOF if $opt{h};
usage: $prog [-p] [file [...]]
       -p: also convert ports to service names
EOF

while (<>) {
  s{
    \b
    ((?:\d{1,3}\.){3}\d{1,3}) # ip
    (?:([:#])(\d+))?          # optional port
    \b
  }{
    ip_to_name($1) . port_to_name($2, $3)
  }gex;
  print;
}

sub ip_to_name {
  my ($ip) = @_;
  gethostbyaddr(inet_aton($ip), AF_INET) || $ip;
}

sub port_to_name {
  my ($sep, $port) = @_;
  if ($sep) {
    $port = getservbyport($port, 'tcp')
          || getservbyport($port, 'udp') || $port if $opt{p};
    return "$sep$port";
  } 
  '';
}
