#!/usr/bin/perl
#
# ssurl - upload a screenshot and get a public url for it
#         TODO: support multiple services, don't do just screenshots
#

use strict;
use warnings;
use Getopt::Std;
use File::Basename;
use utf8;

my $dir = (getpwuid($<))[7] . '/Desktop';
my $num = 1;

my $prog = basename($0);
my %opt;
getopts('hd:n:', \%opt);
usage() if $opt{h} || @ARGV;
sub usage {
  die <<EOU;
usage: $prog [-d dir] [-n num]
       -d: dir to check for new screenshots; default: $dir
       -n: number most recent to upload; default: $num
EOU
}

$dir = $opt{d} if $opt{d};
$num = $opt{n} if $opt{n};

opendir(my $dh, $dir) || die $!;
my @shots = sort {
  (stat("$dir/$b"))[9] <=> (stat("$dir/$a"))[9]
} grep(/^(Screenshot(-\d+)?|screenshot\d+)\.png$/, readdir($dh));
closedir($dh);

splice(@shots, $num);

for my $file (@shots) {
  # TODO: use LWP
  open(my $fh, '-|', 'curl', '-F', "file=\@$dir/$file;type=image/png", '-is',
                             '-F', '__submit=Upload', 'http://simimage.com/')
    || die $!;
  my $res =  do { local $/; <$fh> };
  close($fh);
  $res =~ /HTTP\/1.1 302 Found.+?[lL]ocation:\s+(\S+)/s
    || die "couldn't find redirect\n";
  print "http://simimage.com/f$1\n";
}
