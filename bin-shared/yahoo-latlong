#!/usr/bin/perl

use strict;
use warnings;
use LWP::Simple;
use Getopt::Std;
use CGI '-oldstyle_urls';
use XML::XPath;

my %opt;
getopts('h', \%opt);
die <<EOF if $opt{h} || !@ARGV;
usage: $0 location
EOF

my $url = 'http://api.local.yahoo.com/MapsService/V1/geocode?' . CGI->new({
  appid    => 'dbushong',
  location => join(' ', @ARGV),
})->query_string;

my $xml = get($url) 
  || die "$0: failed to get xml for url: $url\n";

my $p = XML::XPath->new(xml => $xml)
  || die "$0: parse failed for xml: $xml\n";

my $lat  = $p->findvalue('/ResultSet/Result/Latitude/text()');
my $long = $p->findvalue('/ResultSet/Result/Longitude/text()');

print "$lat $long\n";

__END__

<?xml version="1.0" encoding="UTF-8"?>
<ResultSet xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="urn:yahoo:maps" xsi:schemaLocation="urn:yahoo:maps http://api.local.yahoo.com/MapsService/V1/GeocodeResponse.xsd">
 <Result precision="address">
  <Latitude>37.6825</Latitude>
  <Longitude>-122.048643</Longitude>
  <Address>3136 JAMIE WAY</Address>
  <City>HAYWARD</City>
  <State>CA</State>
  <Zip>94541-3500</Zip>
  <Country>US</Country>
 </Result>
</ResultSet>
