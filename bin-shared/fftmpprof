#!/usr/bin/perl
#
# creates a temporary firefox profile, runs firefox, then cleans up on exit
#

use strict;
use warnings;
use Getopt::Std;
use File::Basename;
use Config::IniFiles;
use File::Path;
use POSIX 'strftime';

my $prog = basename($0);
my %opt;
getopts('hc:d:p:Rv', \%opt);
usage() if $opt{h};
sub usage {
  die <<EOU;
usage: $prog [-v] [-c cmd] [-R] [-d dup-profile] [-p profile-name] \\
             [more ff options]
       -c: specify command, e.g. firefox-3.0 or firefox-3.5; default: firefox
       -d: duplicate a given existing profile
       -R: don't remove the profile when done
       -p: specify a name for the profile instead of autogenerating
       -v: verbose
EOU
}

# Initialization
my $ff   = (exists($opt{c}) ? $opt{c} : 'firefox') . ' -no-remote';
my $prof = exists($opt{p}) 
         ? $opt{p} : strftime('tmp-%y%m%d%H%M%S', localtime());

# Create new Profile and capture its path
my $cp = `$ff -CreateProfile $prof 2>&1`;
$cp =~ m{^Success: created profile '$prof' at '([^']+)/([^/]+)/prefs.js'$}m
  || die "$prog: bad output from $ff -CreateProfile $prof: $cp\n";
my ($profiles_dir, $path) = ($1, $2);
print "created profile $prof at $profiles_dir/$path\n" if $opt{v};

# Clone existing over top, if requested
my $ini = "$profiles_dir/profiles.ini";
my $cfg = Config::IniFiles->new(-file => $ini);
if ($opt{d}) {
  my $cpath = $cfg->val(find_profile_section(Name => $opt{d}), 'Path');
  die "DOESN'T WORK";
}

# Run Firefox on the new profile
my $run = "$ff -P $prof";
$run .= ' ' . join(' ', map { quotemeta } @ARGV) if @ARGV;
print "running $run\n" if $opt{v};
exec($run) if $opt{R};
system($run);

# Clear out the tmp profile
print "clearing profile with Path=$path from $ini\n" if $opt{v};
$cfg->DeleteSection(find_profile_section(Path => $path));
$cfg->WriteConfig($ini);

# Delete the profile dir
print "removing $profiles_dir/$path\n" if $opt{v};
rmtree("$profiles_dir/$path");

sub find_profile_section {
  my ($key, $val) = @_;
  (grep { /^Profile\d+$/ && $cfg->val($_, $key) eq $val } $cfg->Sections)[0]
    || die "$prog: couldn't find $key=$val in $ini\n";
}
